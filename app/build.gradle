apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.pdd.booknow"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 100
        versionName "0.1"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')//, 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            res.srcDirs = ['src/main/res/']
            java.srcDirs = ['src/main/java/']
            proto.srcDirs = ['src/main/proto/']
        }
    }
}

final String SUPPORT_LIBS_VERSION = '28.0.0'
final String PROTOBUF_VERSION = '3.6.1'
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:gridlayout-v7:${SUPPORT_LIBS_VERSION}"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    //protobuf "com.google.protobuf:protobuf-java:${PROTOBUF_VERSION}"


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${PROTOBUF_VERSION}"
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}
